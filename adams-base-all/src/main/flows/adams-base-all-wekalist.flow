# Project: adams
# Date: 2016-12-05 09:24:57
# User: fracpete
# Charset: UTF-8
adams.flow.control.Flow -annotation "Processes the Wekalist archives, plotting the number of posts and size of plots in KBs." -flow-execution-listener adams.flow.execution.NullListener
 adams.flow.standalone.CallableActors
  adams.flow.sink.Display -name progress -writer adams.data.io.output.NullWriter
  adams.flow.sink.SequencePlotter -width 1000 -x -3 -writer adams.gui.print.NullWriter -paintlet adams.gui.visualization.sequence.LinePaintlet -overlay-paintlet adams.gui.visualization.sequence.CirclePaintlet -marker-paintlet adams.flow.sink.sequenceplotter.NoMarkers -error-paintlet adams.flow.sink.sequenceplotter.NoErrorPaintlet -mouse-click-action adams.flow.sink.sequenceplotter.NullClickAction -color-provider adams.gui.visualization.core.DefaultColorProvider -overlay-color-provider adams.gui.visualization.core.DefaultColorProvider -title WekaList -axis-x "adams.gui.visualization.core.AxisPanelOptions -label Date -type DATE -tick-generator adams.gui.visualization.core.axis.SimpleTickGenerator -width 40 -custom-format yy-MM" -axis-y "adams.gui.visualization.core.AxisPanelOptions -label Amount -tick-generator adams.gui.visualization.core.axis.SimpleTickGenerator -width 60 -custom-format 0.0" -plot-updater adams.flow.sink.sequenceplotter.SimplePlotUpdater -post-processor adams.flow.sink.sequenceplotter.PassThrough
 adams.flow.standalone.SetVariable -var-name base_url -var-value https://list.waikato.ac.nz/pipermail/wekalist/
 adams.flow.source.Start
 adams.flow.control.Trigger -name "create empty spreadsheet"
  adams.flow.source.NewSpreadSheet -columns "Month,Number of Posts,Size of posts (KB)" -data-row-type adams.data.spreadsheet.DenseDataRow -spreadsheet-type adams.data.spreadsheet.DefaultSpreadSheet
  adams.flow.transformer.SetStorageValue -storage-name sheet
 adams.flow.control.Trigger -name "get index"
  adams.flow.source.CombineVariables -expression @{base_url}index.html
  adams.flow.transformer.DownloadContent
  adams.flow.transformer.Convert -conversion adams.data.conversion.HTMLToDOM
  adams.flow.transformer.XSLT -inline "<?xml version=\\\"1.0\\\"?>\\n\\n<xsl:stylesheet version=\\\"1.0\\\" xmlns:xsl=\\\"http://www.w3.org/1999/XSL/Transform\\\">\\n  <xsl:output method=\\\"text\\\" omit-xml-declaration=\\\"yes\\\" indent=\\\"no\\\"/>\\n  <xsl:template match=\\\"/\\\">\\n    <xsl:for-each select=\\\"//A[text() = \\\'[ Thread ]\\\']\\\">\\n      <xsl:value-of select=\\\"@href\\\"/>\\n      <xsl:text>&#10;</xsl:text>\\n    </xsl:for-each>\\n  </xsl:template>\\n</xsl:stylesheet>"
  adams.flow.transformer.StringSplit -expression \n
  adams.flow.transformer.ArrayToSequence
  adams.flow.control.Tee -name progress
   adams.flow.sink.CallableSink -callable progress
  adams.flow.transformer.SetVariable -var-name thread
  adams.flow.control.Tee -name "determine timestamp"
   adams.flow.transformer.StringReplace -find /thread.html -type SIMPLE
   adams.flow.transformer.StringReplace -name Jan -find -January -replace -01 -type SIMPLE
   adams.flow.transformer.StringReplace -name Feb -find -February -replace -02 -type SIMPLE
   adams.flow.transformer.StringReplace -name Mar -find -March -replace -03 -type SIMPLE
   adams.flow.transformer.StringReplace -name Apr -find -April -replace -04 -type SIMPLE
   adams.flow.transformer.StringReplace -name May -find -May -replace -05 -type SIMPLE
   adams.flow.transformer.StringReplace -name Jun -find -June -replace -06 -type SIMPLE
   adams.flow.transformer.StringReplace -name Jul -find -July -replace -07 -type SIMPLE
   adams.flow.transformer.StringReplace -name Aug -find -August -replace -08 -type SIMPLE
   adams.flow.transformer.StringReplace -name Sep -find -September -replace -09 -type SIMPLE
   adams.flow.transformer.StringReplace -name Oct -find -October -replace -10 -type SIMPLE
   adams.flow.transformer.StringReplace -name Nov -find -November -replace -11 -type SIMPLE
   adams.flow.transformer.StringReplace -name Dec -find -December -replace -12 -type SIMPLE
   adams.flow.transformer.SetVariable -name SetVariable-1 -var-name month
   adams.flow.transformer.Convert -conversion "adams.data.conversion.StringToDateTimeType -format yyyy-MM -datetime-type MSECS"
   adams.flow.transformer.SetVariable -var-name timestamp
  adams.flow.control.Trigger -name "determine number of posts"
   adams.flow.source.CombineVariables -expression @{base_url}@{thread}
   adams.flow.transformer.DownloadContent
   adams.flow.transformer.Convert -conversion adams.data.conversion.HTMLToDOM
   adams.flow.transformer.XPath -expression //LI/A
   adams.flow.transformer.Convert -name Convert-1 -conversion adams.data.conversion.DOMNodeListToArray
   adams.flow.transformer.ArrayLength
   adams.flow.transformer.SetVariable -var-name num_posts
   adams.flow.sink.CallableSink -callable progress
  adams.flow.control.Trigger -name "determine size of posts"
   adams.flow.source.CombineVariables -expression @{base_url}@{thread}
   adams.flow.transformer.StringReplace -find .thread.html$ -replace .txt.gz
   adams.flow.control.Tee -name download
    adams.flow.sink.DownloadFile -output ${TMP}/posts.txt.gz
   adams.flow.control.Trigger -name "determine size"
    adams.flow.source.FileSupplier -file ${TMP}/posts.txt.gz
    adams.flow.transformer.GZIP -remove true -output ${TMP}
    adams.flow.transformer.FileInfo
    adams.flow.transformer.MathExpression -expression "X / 1000"
    adams.flow.transformer.SetVariable -var-name size_posts
    adams.flow.sink.CallableSink -callable progress
   adams.flow.control.Trigger -name "remove files"
    adams.flow.source.DirectoryLister -dir ${TMP} -list-files true -regexp posts.txt.*
    adams.flow.transformer.DeleteFile
  adams.flow.control.Trigger -name "plot number of posts"
   adams.flow.source.VariablesArray -var-name timestamp -var-name num_posts
   adams.flow.control.ArrayProcess
    adams.flow.transformer.Convert -conversion adams.data.conversion.StringToDouble
   adams.flow.transformer.MakePlotContainer -plot-name size
   adams.flow.sink.CallableSink -callable SequencePlotter
  adams.flow.control.Trigger -name "plot size of posts"
   adams.flow.source.VariablesArray -var-name timestamp -var-name size_posts
   adams.flow.control.ArrayProcess
    adams.flow.transformer.Convert -conversion adams.data.conversion.StringToDouble
   adams.flow.transformer.MakePlotContainer -plot-name posts
   adams.flow.sink.CallableSink -callable SequencePlotter
  adams.flow.control.Trigger -name "new row"
   adams.flow.source.StorageValue -storage-name sheet
   adams.flow.transformer.SpreadSheetInsertRow -no-copy true -after true
   adams.flow.transformer.SpreadSheetSetCell -name month -no-copy true -row last -value @{month}
   adams.flow.transformer.SpreadSheetSetCell -name "number of posts" -no-copy true -row last -col 2 -value @{num_posts}
   adams.flow.transformer.SpreadSheetSetCell -name "size of posts" -no-copy true -row last -col 3 -value @{size_posts}
   adams.flow.transformer.SetStorageValue -storage-name sheet
 adams.flow.control.Trigger -name "display spreadsheet"
  adams.flow.source.StorageValue -storage-name sheet
  adams.flow.transformer.SpreadSheetSort -no-copy true
  adams.flow.sink.SpreadSheetDisplay -y -3 -writer adams.data.io.output.NullWriter
