# Project: adams
# Date: 2016-08-06 16:31:51
# User: fracpete
# Charset: UTF-8
adams.flow.control.Flow -annotation "Performs web-scraping on the MLOSS.org website to generate stats for projects.\\nThe user needs to enter the number of project pages (i.e., from most recent to oldest)\\nto process." -error-handling ACTORS_DECIDE_TO_STOP_ON_ERROR -flow-execution-listener adams.flow.execution.NullListener
 adams.flow.standalone.CallableActors
  adams.flow.sink.Display -name raw -writer adams.data.io.output.NullWriter
 adams.flow.source.EnterValue -message "How many pages to process?" -initial-value 1
 adams.flow.transformer.SetVariable -var-name num_pages
 adams.flow.control.Trigger -name "init spreadsheet"
  adams.flow.source.NewSpreadSheet -columns ID,Project,Views,Downloads -data-row-type adams.data.spreadsheet.DenseDataRow -spreadsheet-type adams.data.spreadsheet.DefaultSpreadSheet
  adams.flow.transformer.SetStorageValue -storage-name stats
 adams.flow.control.Trigger -name "iterate pages"
  adams.flow.source.ForLoop -upper @{num_pages}
  adams.flow.transformer.SetVariable -var-name page
  adams.flow.control.Trigger -name info
   adams.flow.source.CombineVariables -expression "--> @{page} / @{num_pages}"
   adams.flow.sink.CallableSink -callable raw
  adams.flow.control.Trigger -name "create url"
   adams.flow.source.CombineVariables -expression http://mloss.org/software/?page=@{page}
   adams.flow.transformer.SetVariable -var-name url
  adams.flow.control.Trigger -name "process page"
   adams.flow.standalone.SetVariable -name "reset project" -var-name project -var-value ?
   adams.flow.standalone.SetVariable -name "reset project id" -var-name projectid -var-value ?
   adams.flow.standalone.SetVariable -name "reset downloads" -var-name downloads -var-value ?
   adams.flow.standalone.SetVariable -name "reset views" -var-name views -var-value ?
   adams.flow.source.URLSupplier -url @{url}
   adams.flow.transformer.DownloadContent
   adams.flow.transformer.StringSplit -expression \n
   adams.flow.transformer.StringMatcher -regexp .*(\\/software\\/view\\/|\\/software\\/viewstats\\/|\\/software\\/downloadstats\\/).*
   adams.flow.transformer.SetVariable -name "reset counter" -var-name count -var-value 0
   adams.flow.transformer.ArrayToSequence
   adams.flow.transformer.IncVariable -var-name count
   adams.flow.transformer.Convert -conversion adams.data.conversion.HTMLToText
   adams.flow.control.Tee -name extract
    adams.flow.control.Switch -condition "adams.flow.condition.bool.Expression -expression \"find(\\\\\\\"downloadstats\\\\\\\", \\\\\\\"X\\\\\\\") > 0\"" -condition "adams.flow.condition.bool.Expression -expression \"find(\\\\\\\"viewstats\\\\\\\", \\\\\\\"X\\\\\\\") > 0\"" -condition adams.flow.condition.bool.True -case adams.flow.sink.Null
     adams.flow.control.Sequence -name downloads
      adams.flow.transformer.StringReplace -find ".* ([0-9]+) downloads.*" -replace $1
      adams.flow.transformer.SetVariable -var-name downloads
     adams.flow.control.Sequence -name views
      adams.flow.transformer.StringReplace -find ".* ([0-9]+) views.*" -replace $1
      adams.flow.transformer.SetVariable -var-name views
     adams.flow.control.Sequence -name project
      adams.flow.control.Tee -name name
       adams.flow.transformer.StringReplace -find ".*([0-9]+)\\\\/ (.*)" -replace $2
       adams.flow.transformer.SetVariable -var-name project
      adams.flow.control.Tee -name id
       adams.flow.transformer.StringReplace -find ".*\\\\/([0-9]+)\\\\/ .*" -replace $1
       adams.flow.transformer.SetVariable -var-name projectid
   adams.flow.control.Branch
    adams.flow.control.ConditionalTrigger -condition "adams.flow.condition.bool.Expression -expression \"@{count} % 3 = 0\""
     adams.flow.source.StorageValue -storage-name stats
     adams.flow.transformer.SpreadSheetInsertRow -no-copy true -after true
     adams.flow.transformer.SpreadSheetSetCell -name "set ID" -no-copy true -row last -col ID -value @{projectid}
     adams.flow.transformer.SpreadSheetSetCell -name "set project" -no-copy true -row last -col Project -value @{project}
     adams.flow.transformer.SpreadSheetSetCell -name "set views" -no-copy true -row last -col Views -value @{views}
     adams.flow.transformer.SpreadSheetSetCell -name "set downloads" -no-copy true -row last -col Downloads -value @{downloads}
     adams.flow.transformer.SetStorageValue -storage-name stats
    adams.flow.sink.CallableSink -callable raw
 adams.flow.control.Trigger -name "display stats"
  adams.flow.source.StorageValue -storage-name stats
  adams.flow.sink.SpreadSheetDisplay -x -3 -allow-search true -writer adams.data.io.output.NullWriter
