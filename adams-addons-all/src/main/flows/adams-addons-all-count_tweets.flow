# Project: adams
# Date: 2018-09-07 19:19:40
# User: fracpete
# Charset: UTF-8
# Modules: adams-access,adams-audio,adams-cntk,adams-cntk-weka,adams-compress,adams-core,adams-cqengine,adams-event,adams-excel,adams-ffmpeg,adams-gnuplot,adams-groovy,adams-groovy-webservice,adams-heatmap,adams-image-webservice,adams-imaging,adams-imaging-boofcv,adams-imaging-imagemagick,adams-imaging-openimaj,adams-jooq,adams-jython,adams-latex,adams-maps,adams-meka,adams-meka-webservice,adams-meta,adams-ml,adams-moa,adams-mongodb,adams-net,adams-nlp,adams-odf,adams-osm,adams-pdf,adams-phmm4weka,adams-python,adams-r,adams-random,adams-rats,adams-rest,adams-rsync,adams-spreadsheet,adams-timeseries,adams-twitter,adams-twitter-rats,adams-video,adams-visualstats,adams-webservice,adams-weka,adams-weka-webservice
#
adams.flow.control.Flow -annotation "Traverses a directory with .csv.gz files containing tweets,\\ncounting the number of rows and outputting a total and \\ncount per file." -flow-execution-listener adams.flow.execution.NullListener -flow-restart-manager adams.flow.control.flowrestart.NullManager
 adams.flow.standalone.CallableActors
  adams.flow.sink.Display -name Count -short-title true -writer adams.data.io.output.NullWriter
 adams.flow.source.Start
 adams.flow.control.Trigger -name "read files"
  adams.flow.standalone.SetVariable -name SetVariable-1 -var-name total -var-value 0
  adams.flow.source.FileSystemSearch -search "adams.flow.source.filesystemsearch.LocalFileSearch -directory /storm/research/backup/twitter -regexp .*.gz -sorting SORT_BY_NAME"
  adams.flow.control.Tee -name "extract file name"
   adams.flow.transformer.BaseName
   adams.flow.transformer.SetVariable -var-name file
  adams.flow.transformer.SetVariable -name SetVariable-2 -var-name current -var-value 0
  adams.flow.control.Tee -name "load file"
   adams.flow.transformer.SpreadSheetFileReader -reader "adams.data.io.input.CsvSpreadSheetReader -data-row-type adams.data.spreadsheet.DenseDataRow -spreadsheet-type adams.data.spreadsheet.DefaultSpreadSheet -missing \"\" -text-columns first-last -chunk-size 10000"
   adams.flow.transformer.SpreadSheetInfo
   adams.flow.transformer.SetVariable -var-name chunk
   adams.flow.transformer.IncVariable -var-name current -inc-int @{chunk}
   adams.flow.transformer.IncVariable -name IncVariable-1 -var-name total -inc-int @{chunk}
  adams.flow.control.Trigger -name "output current"
   adams.flow.source.CombineVariables -expression "@{file}: @{current}" -conversion adams.data.conversion.StringToString
   adams.flow.sink.CallableSink -callable Count
 adams.flow.control.Trigger -name "output total"
  adams.flow.source.CombineVariables -expression "total: @{total}" -conversion adams.data.conversion.StringToString
  adams.flow.sink.CallableSink -callable Count